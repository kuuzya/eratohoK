;---------------------
;基本的な発生確率(1000分率 100で10%)
;これにコンフィグ項目の発生頻度がかけられるので、必ずしもこの通りにはならない
;---------------------
@KOJO_DAILY_K5_FROZEN_RATE(対象)
#DIM 対象
RETURN 50


;---------------------
;確率以外の発生判定
;〇期以降になると発生するとか、デイリー側で利用している変数が-1だったら起こさない　みたいなはじき方をするときに使う
;---------------------
@KOJO_DAILY_K5_FROZEN_DECISION(対象)
#DIM 対象

SIF MASTER == NAME_TO_CHARA("諏訪子")
    RETURN 0

SIF KDVAR:対象:チルノ_蛙冷凍ごっこ
    RETURN 0

RETURN CHECK_KOJO_DAILY_HAPPEN(対象, 1, 0, 1)

;---------------------
;ジャンル
;コンフィグ設定で使用
;---------------------
@KOJO_DAILY_K5_FROZEN_GENRE(対象)
#DIM 対象
RETURN デイリー_ジャンル_その他

;---------------------
;本体
;イベントが発生した場合は1、発生しなかった場合は0を返す
;発生しなかったというのは例えば、特定条件を満たすキャラからランダムに一人選ぶデイリーで、そもそもその条件を満たすキャラが一人もいないみたいなとき
;旧仕様で作ったことある人向けにいうと「旧仕様のデイリー本体冒頭で-1を返すような状況」
;---------------------
@KOJO_DAILY_K5_FROZEN(対象)
#DIM 対象

PRINTFORML 拠点の中庭で%ANAME(対象)%を見かけた
PRINTFORMW 池で何かをしているようだ……
PRINTFORML 「あっ、%ANAME(MASTER)%！」
PRINTFORMW 「何してたかって？　見れば分かるでしょ、蛙冷凍ごっこだよ！」
PRINTFORMW 「%ANAME(MASTER)%もやってみる？」
PRINTFORML 話を聞くと、どうやら蛙を瞬間冷凍させては解凍して遊んでいたらしいが……

CALL ASK_YN("やる", "やめとく")

IF RESULT == 1
    PRINTFORMW 「なーんだ、つまんないの」
    PRINTFORMW 流石に生き物で遊ぶのはいかがなものか
    PRINTFORMW %ANAME(対象)%の誘いを断った……
    KDVAR:対象:チルノ_蛙冷凍ごっこ = -1
    RETURN 1
ENDIF

PRINTFORMW 「そう来なくっちゃ！」
PRINTFORMW %ANAME(対象)%と蛙を凍らせて遊んだ……

CALL SKILL_LEARN_BY_NAME(対象, スキル_ジャンル_TROOP, NO:対象, "瞬間冷凍ビーム")
SIF RESULT
    CALL COLOR_PRINTW(@"%ANAME(対象)%はスキル「瞬間冷凍ビーム」を習得した！", カラー_注意)
KDVAR:対象:チルノ_蛙冷凍ごっこ = 1

RETURN 1
